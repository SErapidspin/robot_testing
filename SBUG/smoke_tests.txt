*** SETTINGS ***
Documentation		A base set of tests to run on sites to test basic functionality 
Resource		resource.txt
Resource		variables.txt
Test Setup		Run Keywords 	Open Browser 	${HOME PAGE}	${BROWSER}
...			AND	Maximize Browser Window
...			AND 	Set Selenium Speed	${DELAY}
...			AND	Set Selenium Timeout	10
...			AND 	Close Newsletter Popup 	${CLOSE NEWSLETTER BUTTON}	${NEWSLETTER OVERLAY}
Force Tags		Smoke
Test Teardown		Close Browser


*** VARIABLES ***



*** TEST CASES ***
Test Successful Signup
	[Tags]	Register	Account
	Go To Login Page
	Click Register Button		${REGISTER BUTTON}
	Register Account Credentials	${FIRST NAME}	${LAST NAME}	${EMAIL ADDRESS}	${VALID PASSWORD}
	User Should Be On Account Dashboard


Test Successful Login
	[Tags]	Login	Account
	Go To Login Page
	Input Username	${CORRECT USERNAME}
	Input Password	${CORRECT PASSWORD}
	Click Button	xpath=//*[@id="send2"]
	User Should Be On Account Dashboard


Test Change Account Address
	[Tags]	Account
	Go To Login Page
	Login To Account	${CORRECT USERNAME}	${CORRECT PASSWORD}
	Travel To Edit Address
	Change Address		${NEW FIRST NAME}	${NEW LAST NAME}	${NEW TELEPHONE}	${NEW STREET}	${NEW CITY}	${NEW REGION}	${NEW ZIP}
	Check Account Address Was Changed	${NEW FIRST NAME}	${NEW LAST NAME}	${NEW TELEPHONE}	${NEW STREET}	${NEW CITY}	${NEW REGION}	${NEW ZIP}
#Teardown for these types of tests should involve changing the address back to the original values
	Travel To Edit Address
	Change Address		${FIRST NAME}	${LAST NAME}	${TELEPHONE}	${STREET}	${CITY}		${REGION}	${ZIP}

#Should add change password also
Test Change Account Details
	[Tags]	Account
	Go To Login Page
	Login To Account	${CORRECT USERNAME}	${CORRECT PASSWORD}
	User Should Be On Account Dashboard
	Travel To Account Information Page
	Change Account Info	${NEW FIRST NAME}	${NEW LAST NAME}
	Check Account Info Was Changed	${NEW FIRST NAME}	${NEW LAST NAME}	
	Travel To Account Information Page
	Change Account Info	${FIRST NAME}	${LAST NAME}
	Check Account Info Was Changed	${FIRST NAME}	${LAST NAME}


Test General Search
	[Tags]	Search	Listing
	Search For Term		${SEARCH BAR}	${GENERAL SEARCH TERM}
	Click First Product
	Check That Product Is Related	${GENERAL SEARCH TERM}

Test Specific Search
	[Tags]	Search	Listing
	Go To Listing Page     ${LISTING PAGE}
	${SPECIFIC PRODUCT}= 	Get Name Of First Product
	Search For Term		${SEARCH BAR}	${SPECIFIC PRODUCT}
	Check That Product Is On Listing Page	${SPECIFIC PRODUCT}

Test Sort By Name
	[Tags]	sort	Listing
	Go To Listing Page     ${LISTING PAGE}
	Sort Listing Page 	Name
	Check Page Is Sorted By Name	

Test Sort By Price
	[Tags]	sort	Listing
	Go To Listing Page     ${LISTING PAGE}
	Sort Listing Page	Price
	Check Page Is Sorted By Price 

Test Filter By Brand
	[Tags]	filter	brand	Listing
	Go To Listing Page     ${LISTING PAGE}
	Filter Page By Brand	${BRAND FILTER}
	Check Page Is Filtered By Brand		${BRAND FILTER}

Test Filter By Price
	[Tags]	filter	price	Listing
	Go To Listing Page     ${LISTING PAGE}
	Filter Page By Price	${MAXIMUM PRICE}	${MINIMUM PRICE}	
	Check Page Is Filtered By Price		${MAXIMUM PRICE}	${MINIMUM PRICE}


Test Add Simple Product To Cart
	[Tags]	Add	Simple	ProductPage
	Go To Product Page	${SIMPLE PRODUCT}
	Add Simple Product To Cart	${QUANTITY}
	Check Add To Cart Confirmation
	${PRODUCT NAME} =	Get Product Name
	Travel To Cart Page
	Check Product Was Added To Cart		${QUANTITY}	${PRODUCT NAME}
	
Test Add Configurable Product To Cart
	[Tags]	Add	Configurable	ProductPage
	Go To Product Page	${CONFIGURABLE PRODUCT}
	Add Configurable Product To Cart	${QUANTITY}	${CONFIGURATION OPTION}
	Check Add To Cart Confirmation
	${PRODUCT NAME} = 	Get Product Name
	Travel To Cart Page
	Check Product Was Added To Cart		${QUANTITY}	${PRODUCT NAME}

#write keywords
Test Add From Quickview
	[Tags]	Add	Listing
	Go To Listing Page     ${LISTING PAGE}			
	Open Quickview			1	
	Switch To Quickview Frame
	${PRODUCT NAME} =	Get Quickview Product Name
	Add Simple Product To Cart     ${QUANTITY} 
	Unselect Frame
	Travel To Cart Page
	Check Product Was Added To Cart		${QUANTITY}	${PRODUCT NAME}

Test Remove Simple Product From Cart
	[Tags]	Remove	Cart
	Go To Product Page	${SIMPLE PRODUCT}
	${PRODUCT NAME} =	Get Product Name
	Add Simple Product To Cart	${QUANTITY}
	Travel To Cart Page
	Remove Product From Cart	${PRODUCT NAME}
	Check Product Was Removed From Cart	${PRODUCT NAME}

Test Update Simple Product Quantity
	[Tags]	Update	Cart
	Go To Product Page	${SIMPLE PRODUCT}
	${PRODUCT NAME}=	Get Product Name
	Add Simple Product To Cart	${QUANTITY}
	Go To Cart Page		${CART URL}
	Change Product Quantity In Cart		${PRODUCT NAME}		${NEW QUANTITY}
	Check Product Quantity In Cart	${PRODUCT NAME}		${NEW QUANTITY}

# To Do
Test Logged In Checkout 
	[Tags]		LoggedIn	Checkout
	Log		Test
	Go To Product Page	${SIMPLE PRODUCT}
	Add Simple Product To Cart	${QUANTITY}	
	Check Add To Cart Confirmation
	Go To Checkout Page
	Log In On Checkout Page		${EMAIL ADDRESS}	${CORRECT PASSWORD}
	Enter Billing Information	${FIRST NAME}   ${LAST NAME}    ${NEW TELEPHONE}    ${NEW STREET}	${NEW CITY}	${NEW REGION}	${NEW ZIP}
#	Since 'use billing address as shipping address' checkbox is always default clicked, will have to add a new function to unclick it
#	Enter Shipping Information	${FIRST NAME}   ${LAST NAME}    ${EMAIL ADDRESS}        ${NEW TELEPHONE}    ${NEW STREET}       ${NEW CITY}         ${NEW REGION}       ${NEW ZIP}
	Enter Shipping Method
	Enter Payment Information	${CREDIT CARD NUMBER}   ${CREDIT CARD TYPE}     ${CREDIT CARD MONTH}    ${CREDIT CARD YEAR}     ${CREDIT CARD VERIFICATION}
	Wait Until Element Is Visible	id=checkout-step-review	7
#Go back to the cart page and remove the added product from the cart for future tests. Prevents complications with out of stock products down the line. 
	[Teardown]	Run Keywords	Go To Cart Page		${CART URL}
	...		AND		Remove Product From Cart	\
	...		AND		Close Browser

#To Do: add password for guest checkout
Test Checkout As Guest
	[Tags]	Checkout
	Go To Product Page	${SIMPLE PRODUCT}
	Add Simple Product To Cart	${QUANTITY}
	Check Add To Cart Confirmation
	Go To Checkout Page
	Click Element	xpath=//span[text() = "Continue as Guest"]
#Remember to put a new email address, the checkout will throw an error if you're using an email that already has an account
	Enter Billing Information As Guest	${FIRST NAME}   ${LAST NAME}     apang+99@demacmedia.com       ${NEW TELEPHONE}    ${NEW STREET}       ${NEW CITY}         ${NEW REGION}       ${NEW ZIP}	${CORRECT PASSWORD}
	Enter Shipping Method
	Enter Payment Information	${CREDIT CARD NUMBER}   ${CREDIT CARD TYPE}     ${CREDIT CARD MONTH}    ${CREDIT CARD YEAR}     ${CREDIT CARD VERIFICATION}
	Wait Until Element Is Visible	id=checkout-step-review	7


#Write keywords for
Test Critical Path As Guest
	[Tags]	Checkout
	Travel To Listing Page
# Should add conditionals to add first product from quickview and first product to cart in case the product at the position is not a simple product
	Add Simple Product To Cart From Quickview	1	${QUANTITY}	
	Travel To Product Page				1
	Add Simple Product To Cart				${QUANTITY}
	Check Add To Cart Confirmation			
	Travel To Cart Page
	Travel To Checkout Page
	Click Element	xpath=//span[text() = 'Continue as Guest']
	Enter Billing Information As Guest	${FIRST NAME}   ${LAST NAME}    apang+99@demacmedia.com        ${NEW TELEPHONE}    ${NEW STREET}     ${NEW CITY}         ${NEW REGION}       ${NEW ZIP}  ${CORRECT PASSWORD}
#Since 'use billing address as shipping address' checkout is always default clicked, will have to add a new function to unclick it
#	Enter Shipping Information	${FIRST NAME}   ${LAST NAME}   ${NEW TELEPHONE}    ${NEW STREET}       ${NEW CITY}        	 ${NEW REGION}       ${NEW ZIP}  
	Enter Shipping Method
	Enter Payment Information	${CREDIT CARD NUMBER}   ${CREDIT CARD TYPE}     ${CREDIT CARD MONTH}    ${CREDIT CARD YEAR}     ${CREDIT CARD VERIFICATION}
	Place Order
	Order Should Be Successful	${ORDER SUCCESS URL}	
